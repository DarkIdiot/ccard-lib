# swig for php make and install
import os
import commands

env = Environment(
        PREFIX = GetOption('prefix'),
        ENV = {'PATH' : os.environ['PATH']},
        SWIGFLAGS=['-php'],
        SHLIBPREFIX=""
        )
env["CC"] = os.getenv("CC") or env["CC"]
env["CXX"] = os.getenv("CXX") or env["CXX"]
env["ENV"].update(x for x in os.environ.items() if x[0].startswith("CCC_"))

env.ParseConfig("php-config --includes")
phpLibExtDir = '$PREFIX/' + commands.getoutput("php-config --extension-dir")

link_flags = []
# add extra linking options under OSX
if env['PLATFORM'] == 'darwin':
    link_flags.append('-Wl,-undefined,dynamic_lookup')

swigSl = env.SharedLibrary(
        '../php_card.so',
        ['php_ccard.i'],
        LIBS=['ccard-lib'],
        LIBPATH=['#'],
        CCFLAGS=["-Iinclude"],
        LINKFLAGS=link_flags,
        )

phpLib = env.Install(phpLibExtDir, [swigSl])
env.Alias('install-php', phpLib)
env.Clean(swigSl, ["ccard.php", "php_ccard.h"])

# vi:ft=python ts=4 sw=4 et fdm=marker
